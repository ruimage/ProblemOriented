#Использовать v8runner
#Использовать cmdline
#Использовать ParserFileV8i

Функция ПолучитьМассивИменПодключенныхБаз()
	
	МассивИменПодключенныхБаз = Новый Массив;
	
	Парсер = Новый ПарсерСпискаБаз;
	Парсер.УстановитьФайл();
	СписокБаз = Парсер.ПолучитьСписокБаз();
	
	Для каждого База Из СписокБаз Цикл
		СтруктураАдреса = База.Значение;
		Если СтруктураАдреса.Свойство("Connect") Тогда
			МассивИменПодключенныхБаз.Добавить(СтруктураАдреса.Name);
		КонецЕсли;
	КонецЦикла;
	
	Возврат МассивИменПодключенныхБаз;
КонецФункции


Функция ПолучитьМассивАртефактовНаСетевомХранилище()
	МассивАртефактов = Новый Массив();
	
	СписокФайлов = НайтиФайлы("\\gold585.int\techfiles\Артефакты тестовый стенд МФ\", "*.dt", Истина);
	
	Для каждого ТекЭлемент Из СписокФайлов Цикл
		СтруктураФайлаБазы = Новый Структура();
				
		Слова = СтрРазделить(ТекЭлемент.ИмяБезРасширения, " ");
		КлючЗадачи = Слова[0];
		ОчищенноеИмяБазы = СокрЛП(СтрЗаменить(ТекЭлемент.ИмяБезРасширения,КлючЗадачи,""));
		СтруктураФайлаБазы.Вставить("КлючЗадачи",КлючЗадачи);
		СтруктураФайлаБазы.Вставить("ИмяБазы", СокрЛП(ТекЭлемент.ИмяБезРасширения));		
		СтруктураФайлаБазы.Вставить("ОчищенноеИмяБазы",ОчищенноеИмяБазы);
		СтруктураФайлаБазы.Вставить("ПутьФайлаБазы", ТекЭлемент.ПолноеИмя);
		
		МассивАртефактов.Добавить(СтруктураФайлаБазы);
		
	КонецЦикла;
	
	
	Возврат МассивАртефактов;
КонецФункции


Функция ПолучитьПутьКаталогаАртефактов(НомерЗадачи)
	
	ПутьФайлВыгрузки = "\\gold585.int\techfiles\Артефакты тестовый стенд МФ";
	ПутьФайлВыгрузки = ОбъединитьПути(ПутьФайлВыгрузки, НомерЗадачи);
	ПутьФайлВыгрузки = ОбъединитьПути(ПутьФайлВыгрузки, "Тестирование");
	
	Возврат ПутьФайлВыгрузки;
	
КонецФункции

Функция ПолучитьНаименованиеФайлаВыгрузки(НомерЗадачи)
	
	Возврат "" + НомерЗадачи + ".dt";
	
КонецФункции

Функция ПолучитьПутьХраненияТестовыхБаз()
	
	Возврат "D:\Базы";
	
КонецФункции

Процедура СоздатьФайловуюБазуИзФайлаВыгрузки(ИмяБазы,ПутьФайлаБазы)
	
	УправлениеКонфигуратором = Новый УправлениеКонфигуратором();
	
	ПутьБД = ОбъединитьПути(ПолучитьПутьХраненияТестовыхБаз(), ИмяБазы);
	
	Если ЗначениеЗаполнено(ПутьФайлаБазы) Тогда
		
		СоздатьФайловуюБазуПоИмени(УправлениеКонфигуратором, ПутьБД, ИмяБазы);
		
		ЗагрузитьИнформационнуюБазу(УправлениеКонфигуратором, ПутьФайлаБазы, ПутьБД);
		
	Иначе
		Сообщить("Не найдено указанной базы в каталоке артефактов");
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗагрузитьИнформационнуюБазу(УправлениеКонфигуратором, ПутьФайлаБазы, ПутьБД)
	Сообщить("" + ТекущаяДата() + ": Загрузка файла начата: " + ПутьФайлаБазы + " в базу " + ПутьБД);
	УправлениеКонфигуратором.ЗагрузитьИнформационнуюБазу(ПутьФайлаБазы);
	Сообщить("" + ТекущаяДата() + ": Загрузка файла завершена: " + ПутьФайлаБазы + " в базу " + ПутьБД);
КонецПроцедуры

Процедура СоздатьФайловуюБазуПоИмени(УправлениеКонфигуратором, ПутьБД, ИмяБазы)
	
	МассивПодключенныхБаз = ПолучитьМассивИменПодключенныхБаз();
	
	УправлениеКонфигуратором.УстановитьКонтекст("/F """ + ПутьБД + """", "", "");
	Если МассивПодключенныхБаз.Найти(ИмяБазы) = Неопределено Тогда
		Сообщить("" + ТекущаяДата() + ": Создание и подключение базы начато: " + ПутьБД);
		УправлениеКонфигуратором.СоздатьФайловуюБазу(ПутьБД, , ИмяБазы);
	Иначе
		Сообщить("" + ТекущаяДата() + ": Пересоздания базы начато: " + ПутьБД);
		УправлениеКонфигуратором.СоздатьФайловуюБазу(ПутьБД);
	КонецЕсли;
	Сообщить("" + ТекущаяДата() + ": Создание базы завершено: " + ПутьБД);
	
КонецПроцедуры

Процедура СинхронизироватьАртефакты()
	Сообщить("" + ТекущаяДата() + ": Синхронизация список баз запущена");
	//Получить список баз на тестовом стенде
	МассивИменПодключенныхБаз = ПолучитьМассивИменПодключенныхБаз();
	Сообщить("Найдены подключенные базы:");
	Для каждого ТекЭлемент Из МассивИменПодключенныхБаз Цикл
		Сообщить("	"+ТекЭлемент);
	КонецЦикла;
	//Получить список баз на артефактах
	МассивАртефактов = ПолучитьМассивАртефактовНаСетевомХранилище();
	Сообщить("Найдены базы из артефактов:");
	Для каждого ТекСтрк Из МассивАртефактов Цикл
		Сообщить("	"+ТекСтрк.ИмяБазы);
	КонецЦикла;
	//подключить базы отсутсвующие в списках
	Для каждого ТекСтрк Из МассивАртефактов Цикл
		Если НЕ ЕстьБазаВПодключенных(ТекСтрк.ИмяБазы,МассивИменПодключенныхБаз) Тогда		
			СоздатьФайловуюБазуИзФайлаВыгрузки(ТекСтрк.ИмяБазы, ТекСтрк.ПутьФайлаБазы);				
		КонецЕсли;		
	КонецЦикла;	

	Сообщить("" + ТекущаяДата() + ": Синхронизация список баз завершена");
	
КонецПроцедуры

Функция ЕстьБазаВСпискахАртефатов(ИмяПодключеннойБазы, МассивАртефактов)
	
	Если МассивАртефактов.Найти(ИмяПодключеннойБазы) = Неопределено Тогда
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;

КонецФункции

Функция ЕстьБазаВПодключенных(ИмяБазы, МассивИменПодключенныхБаз)
	
	Если МассивИменПодключенныхБаз.Найти(ИмяБазы) = Неопределено Тогда
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;

КонецФункции

Процедура ВыполнениеКоманды()
	
	СинхронизироватьАртефакты();
	
КонецПроцедуры

ВыполнениеКоманды();